#version 460

#extension GL_EXT_debug_printf : enable

#define FLT_MAX 3.402823466e+38
#define FLT_MIN 1.175494351e-38
#define PI 3.14159265358979323846264338327950288

#define MAX_STACK_DEPTH 2048
#define MAX_BOUNCES 2

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

#define TYPE_SPHERE 0
#define TYPE_PLANE 1
#define TYPE_MASK 0xF0000000
#define INDEX_MASK ~TYPE_MASK

struct Sphere {
  vec4 position_radius;
};
struct Plane {
  vec4 position;
  vec4 normal;
};
// For a sphere data1 = position, radius
// For a plane data1 = position, data2 = normal
struct Primitive {
  vec4 color;
  uvec4 type;
};

struct CameraInfo {
  vec4 position_fov;
  vec4 direction;
  vec2 image_size;
};

layout(set = 0, binding = 0, std140) readonly buffer Scene {
  uint num_prims;
  uint pad0;
  uint pad1;
  uint pad2;
  Primitive[] prims;
};
layout(set = 0, binding = 1, rgba8) uniform image2D output_image;

layout(set = 0, binding = 2, std140) readonly buffer SpheresBlock {
  Sphere[] spheres;
};

layout(set = 0, binding = 3, std140) readonly buffer PlanesBlock {
  Plane[] planes;
};

layout(push_constant) uniform CameraData { CameraInfo camera; };

struct HitResult {
  bool intersection;
  float t;
  vec3 intersection_point;
  vec3 hit_normal;
};

struct Ray {
  vec3 ray_origin;
  vec3 ray_direction;
  int parent;
  int bounces;
};

/*
Gets the Van der Corput representation of x, which is
a decimal number with x's binary representation mirrored as the decimal part.

e.g x = 154 = 0b10011010, so VdC(152) = 0.01011001
The formula is

VdC(x) = a_0 / 2 + ... + a_i / 2^i + ... + a_r / 2^r+1
where a_i are factors in the binary representation of x
*/
float van_der_corput_representation(uint x) {
  x = (x << 16u) | (x >> 16u);
  x = ((x & 0x55555555u) << 1u) | ((x & 0xAAAAAAAAu) >> 1u);
  x = ((x & 0x33333333u) << 2u) | ((x & 0xCCCCCCCCu) >> 2u);
  x = ((x & 0x0F0F0F0Fu) << 4u) | ((x & 0xF0F0F0F0u) >> 4u);
  x = ((x & 0x00FF00FFu) << 8u) | ((x & 0xFF00FF00u) >> 8u);
  return float(x) * 2.3283064365386963e-10; // / 0x100000000
}

// Returns vec2(x / num_samples, VdC(x)), useful to e.g
// This can be useful to e.g do "randomized" sampling on an hemisphere, since
// the returned numbers are in the [0, 1) range (and can be thus converted to
// hemispherical coords)
vec2 hammersley(uint x, uint num_samples) {
  return vec2(float(x) / float(num_samples), van_der_corput_representation(x));
}

vec3 hemisphereSample_cos(float u, float v) {
  float phi = v * 2.0 * PI;
  float cosTheta = sqrt(1.0 - u);
  float sinTheta = sqrt(1.0 - cosTheta * cosTheta);
  return vec3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);
}

// ray_normal must be normalized
HitResult ray_sphere_intersection(vec3 ray_origin, vec3 ray_normal,
                                  vec3 sphere_origin, float sphere_radius) {
  HitResult result;
  result.intersection = false;
  result.intersection_point = vec3(0.0);
  result.hit_normal = vec3(0.0);
  result.t = 0.0;

  vec3 D = (sphere_origin - ray_origin);
  float d_len = length(D);
  float dd = d_len * d_len;
  float rr = sphere_radius * sphere_radius;

  float theta = dot(ray_normal, D);
  float delta = theta * theta - dd + rr;

  if (delta < 0.0) {
    return result;
  } else if (delta < 0.005) {
    result.intersection = true;
    result.t = theta;
  } else {
    result.intersection = true;
    float sqrt_d = sqrt(delta);
    float t1 = theta + sqrt_d;
    float t2 = theta - sqrt_d;
    result.t = min(t1, t2);
  }

  if (result.t > 0.0) {
    result.intersection_point = ray_origin + ray_normal * (result.t + 0.01);
    result.hit_normal = normalize(result.intersection_point - sphere_origin);
  }
  return result;
}

// ray_normal must be normalized
HitResult ray_plane_intersection(vec3 ray_origin, vec3 ray_normal,
                                 vec3 plane_origin, vec3 plane_normal) {
  HitResult result;
  result.intersection = false;
  result.intersection_point = vec3(0.0);
  result.hit_normal = plane_normal;
  result.t = 0.0;

  float bottom = dot(ray_normal, plane_normal);
  if (bottom >= 0.005) {
    result.intersection = true;
    float top = -dot(plane_normal, plane_origin - ray_origin);
    result.t = top / bottom;
  }

  if (result.t > 0.0) {
    result.intersection_point = ray_origin + ray_normal * (result.t + 0.01);
  }

  return result;
}

vec3 get_camera_dir() {
  uvec2 image_size = imageSize(output_image);
  vec2 st =
      (vec2(gl_GlobalInvocationID.xy) + vec2(0.5) - vec2(image_size) * 0.5) /
      vec2(image_size * 0.5);
  float aspect_ratio = float(camera.image_size.x) / float(camera.image_size.y);
  float fovy_rads = camera.position_fov.w;
  st = vec2(st.x * aspect_ratio, st.y) * tan(fovy_rads / 2.0);

  vec3 camera_dir = normalize(camera.direction.xyz + vec3(st.x, st.y, 0.0));
  return camera_dir;
}

uint primitive_type(int prim_index) {
  Primitive prim = prims[prim_index];
  return (prim.type.x & TYPE_MASK) >> 28;
}
uint primitive_index(int prim_index) {
  Primitive prim = prims[prim_index];
  return (prim.type.x & INDEX_MASK);
}

HitResult find_closest_hit(vec3 origin, vec3 direction, inout int best_prim) {
  HitResult hit;
  hit.intersection = false;
  hit.t = FLT_MAX;

  for (int i = 0; i < num_prims; i++) {
    Primitive prim = prims[i];
    HitResult current_hit;

    uint prim_type = primitive_type(i);
    uint prim_ty_index = primitive_index(i);

    if (prim_type == TYPE_SPHERE) {
      Sphere sphere = spheres[prim_ty_index];
      current_hit =
          ray_sphere_intersection(origin, direction, sphere.position_radius.xyz,
                                  sphere.position_radius.w);
    } else if (prim_type == TYPE_PLANE) {
      Plane plane = planes[prim_ty_index];
      current_hit = ray_plane_intersection(
          origin, direction, plane.position.xyz, plane.normal.xyz);
    }
    if (current_hit.intersection && current_hit.t < hit.t) {
      hit = current_hit;
      best_prim = i;
    }
  }

  return hit;
}

void main() {
  uvec2 stats = uvec2(0);
  uvec2 image_size = imageSize(output_image);

  if (gl_GlobalInvocationID.x > image_size.x ||
      gl_GlobalInvocationID.y > image_size.y) {
    return;
  }

  vec3 ray_direction = get_camera_dir();
  vec3 ray_origin = camera.position_fov.xyz;
  vec3 pixel_color = vec3(0.0);
  float contribute = 1.0;

  int prim_index = -1;
  int i = 0;
  bool continue_trace = true;
  while (continue_trace && i <= MAX_BOUNCES && contribute >= 0.005) {
    HitResult hit = find_closest_hit(ray_origin, ray_direction, prim_index);
    if (!hit.intersection) {
      continue_trace = false;
    } else {

      uint prim_type = primitive_type(prim_index);

      pixel_color += prims[prim_index].color.rgb * contribute *
                     (prim_type == TYPE_SPHERE
                          ? clamp(dot(hit.hit_normal, -ray_direction), 0.0, 1.0)
                          : 1.0);
      contribute *= 0.5;
      ray_origin = hit.intersection_point;
      ray_direction = reflect(ray_direction, hit.hit_normal);
      stats.x += 1;
    }
    i++;
  }

  debugPrintfEXT("Hits %d", stats.x);

  imageStore(output_image,
             ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y),
             vec4(pixel_color, 1.0));
}